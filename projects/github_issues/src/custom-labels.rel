/*****
 * Relations that are specific to labels in the repo.
 */

// Get label categories, designated by a split character in the label name

def label_split_char = ':'

def regex_label_split_char = string_join[ "|", sort[string[label_split_char]] ]

def label_name[:category][label_id] = substring[label_string,1,ind-1]
    from label_string, ind, spl_char where
    label_name(label_id,label_string) and
    char(label_string,ind,spl_char) and
    label_split_char(spl_char) and
    not regex_match[regex_label_split_char, substring[label_string,1,ind-1]]

def label_category = label_name[:category][_]

def label_name[:value][label_id] = substring[label_string, ind, num_chars[label_string]]
    from label_string, ind, cat_str where
    label_name(label_id,label_string) and
    label_name[:category](label_id,cat_str) and
    ind = num_chars[cat_str] + 2   

// Get labels that 

 /*****
 * Labels (currently collected from labels that occur in issues)
 */

ic { customer_label ⊆ Label }
ic { current_customer_label ⊆ customer_label }

def customer_label(l) =
    label_name(l, s) and
    regex_match("^customer", s)
    from s

def current_customer_label(l) =
    label_name(l, "customer:att") or
    label_name(l, "customer:ey") or
    label_name(l, "customer:lantik")

def priority_urgent_label(l) =
    label_name(l, "priority:urgent")

def priority_high_label(l) =
    label_name(l, "priority:high")

def priority_medium_label(l) =
    label_name(l, "priority:medium")

def priority_low_label(l) =
    label_name(l, "priority:low")

/*****
 * Priorities
 */
def priority_urgent(i) =
    has_label(i, l)
    from l in priority_urgent_label

def priority_high(i) =
    has_label(i, l)
    from l in priority_high_label

def priority_medium(i) =
    has_label(i, l)
    from l in priority_medium_label

def priority_low(i) =
    has_label(i, l)
    from l in priority_low_label

def priority_missing(i) =
    issue(i) and not (
        priority_urgent(i) or
        priority_high(i) or
        priority_medium(i) or
        priority_low(i))

def priority[i in priority_urgent] = "urgent"
def priority[i in priority_high] = "high"
def priority[i in priority_medium] = "medium"
def priority[i in priority_low] = "low"
def priority[i in priority_missing] = "-"

/*****
 * Components
 */
def component_label(l) =
    label_name(l, s) and
    regex_match("^component:", s)
    from s